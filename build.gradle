buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE")
    }
}

plugins {
    id 'com.moowork.node' version '1.1.1'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'idea'

group = 'com.hots'
version = '1.0.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/libs-release"}
}

jar.dependsOn('buildFrontend')

jar {
    from('ui/public') {
        into 'static'
    }
}

task installNpm(type: NpmTask) {
    execOverrides {
        it.workingDir = 'ui'
    }
    args = ['install']
}

task buildFrontend(type: NpmTask, dependsOn: 'installNpm') {
    execOverrides {
        it.workingDir = 'ui'
    }
    args = ['run', 'build']
}

task copyCerts(type: Copy) {
    from file("./keystore.p12")
    into file("build")
}

build.dependsOn('copyCerts')

dependencies {
    compileOnly('org.projectlombok:lombok:1.16.20')

    compile 'org.springframework.boot:spring-boot-starter-web:1.5.8.RELEASE'
    compile 'org.springframework.security.oauth:spring-security-oauth2:2.0.11.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:1.5.8.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-security:1.5.8.RELEASE'

    compile group: 'org.hibernate', name: 'hibernate-gradle-plugin', version: '5.3.0.CR1'
    compile files('catalano/Catalano.Core.jar')
    compile files('catalano/Catalano.Math.jar')
    compile files('catalano/Catalano.Neuro.jar')
    compile files('catalano/Catalano.Statistic.jar')
    compile group: 'org.postgresql', name: 'postgresql', version:'42.1.4'
}
